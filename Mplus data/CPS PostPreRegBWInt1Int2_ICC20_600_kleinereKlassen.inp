TITLE:	Monte Carlo Simulation
for Transfer Effect
of Intervention on Complex Problem Solving
Assumptions:
- Intraclass correlation coefficient = .20
- Pretest explains 89% of between classroom variance at posttest
- Intervention explains 10% of between classroom variance at posttest
- 5% between classroom variance left unexplained at posttest
- 5% Missing data on posttest

montecarlo:
		names are post pre est mint;
		nobservations = 600;
		ncsizes = 4;
		csizes = 5 (10) 10 (15) 15(20) 4 (25);
        cutpoints = est (.5) mint (.5);
        PATMISS = post (.05);
		PATPROBS = 1;
        seed = 666;
		nreps = 1000;
        between = est mint;
		save = MCTransfer_*.dat;
        repsave = all;

ANALYSIS:   TYPE = TWOLEVEL;
            !ESTIMATOR = MLR;

model population:
		
        %within%
        post*1.3;
        pre*4;
        post ON pre*.894;        

        %between%
        post*.05;
        pre*1;
        [est*.5];
        est*.25;
        [mint*.5];
        mint*.25;
        post ON pre*1 est*.447 mint*.447;
        		
model:

        %within%
        post*1.3;

        post ON pre*.894;

        %between%
        post*.05;



        post ON pre*1 est*.447 mint*.447;



output:
		tech9;