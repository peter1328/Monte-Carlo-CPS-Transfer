TITLE:	Monte Carlo Simulation
for Transfer Effect
of Intervention on Complex Problem Solving
Assumptions:
- Intraclass correlation coefficient = .20
- Pretest explains 89% of between classroom variance at posttest
- Intervention explains 10% of between classroom variance at posttest
- 5% between classroom variance left unexplained at posttest
- 5% Missing data on posttest

montecarlo:
  		names are post pre est mint;
  		nobservations = 500000;
  		ncsizes = 6;
        ! Classroom sample: Overall 40 Classrooms with 10 - 30 children
  		csizes = 2 (10) 6 (15)  9 (20)  6 (25)  2 (30) 1(499500); ! Number and size of the 40 classrooms
        ! Define intervention variable (>.5 = intervention group; see 2010 Mediation-article)
        cutpoints = est (.5) mint (.5);
        ! Define missing values
        PATMISS = post (.10); ! 10% of posttest-data on Complex Problem Solving are missing
  		PATPROBS = 1;
        ! Define seed for quasi-random number generation
        seed = 666;
        ! Define number of Monte Carlo draws from the population
  		nreps = 1;
        ! Define variables on second level (school class level)
          between = est mint; ! The interventions happen on the classroom level
        !save = MCTransfer_*.dat; ! Activate to save sampled data
        !repsave = all;         ! Activate to save sampled data

ANALYSIS:   TYPE = TWOLEVEL;
            !ESTIMATOR = MLR;

model population:
		
        %within%
        ! Posttest-residual variance
        post*1.3; ! Retest-reliability .80 20% variance not explained by pre-test
        pre*4; ! Pretest-variance
        post ON pre*.894;        

        %between%
        post*.10;
        pre*.5;
        [est*.5];
        est*.25;
        [mint*.5];
        mint*.25;
        post ON pre*1 est*.447 mint*.447;
        		
model:

        %within%
        post*1.3;

        post ON pre*.894;

        %between%
        post*.10;



        post ON pre*1 est*.447 mint*.447;



output:
		tech9;